plugins {
    id "idea"
    id "java"
    id "com.google.protobuf" version "0.8.6"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
}

version = project.property("api.version")

sourceCompatibility = 1.8
targetCompatibility = 1.8

def grpcVersion = "1.16.1"

dependencies {
    implementation("com.google.api.grpc:proto-google-common-protos:1.0.0")
    implementation("io.grpc:grpc-netty-shaded:${grpcVersion}")
    implementation("io.grpc:grpc-protobuf:${grpcVersion}")
    implementation("io.grpc:grpc-stub:${grpcVersion}")
    implementation("com.salesforce.servicelibs:reactor-grpc-stub:0.9.0")
    implementation("io.projectreactor:reactor-core:3.2.2.RELEASE")

    testImplementation("io.grpc:grpc-testing:${grpcVersion}") {
        exclude group: "junit", module: "junit"
    }
    testImplementation("io.projectreactor:reactor-test:3.2.2.RELEASE")
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.6.1"
    }
    //noinspection GroovyAssignabilityCheck
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:0.8.0:jdk8@jar"
        }
    }
    generateProtoTasks {
        ofSourceSet("main")*.plugins {
            grpc {}
            reactor {}
        }
    }
}

apply from: rootProject.file("gradle/publish-bintray.gradle")